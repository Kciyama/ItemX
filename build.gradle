plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

group = 'org.btc'
version = '1.4.2'
archivesBaseName = 'ItemX'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'SonaType'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'CodeMC'
        url = 'https://repo.codemc.org/repository/maven-public/'
    }
    maven {
        name = 'BG-SoftPublicsLibs'
        url = 'https://repo.bg-software.com/repository/public-libs/'
    }
    maven {
        name = 'PaperMC'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'PlaceholderAPI'
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    implementation 'org.jetbrains:annotations:23.0.0'
    compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'com.comphenix:ProtocolLib:latest'
    compileOnly 'io.lumine:MythicMobs:5.0.1'
    compileOnly 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.10.10'
    compileOnly fileTree(dir: 'depend', includes: ['*.jar'])
    implementation('de.tr7zw:item-nbt-api-plugin:2.10.0')
}

def targetJavaVersion = 16
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    manifest {
        attributes(
                'Main-Class': 'org.btc.itemx.ItemX'
        )
    }
}